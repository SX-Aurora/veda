macro(ve_init_information_ext LANG)
	set(CMAKE_STATIC_LIBRARY_SUFFIX_${LANG} .va)
	set(CMAKE_SHARED_LIBRARY_SUFFIX_${LANG} .vso)
endmacro()

macro(ve_init_information LANG X LANG_ENV)
	set(CMAKE_${LANG}_OUTPUT_EXTENSION .o)
	set(CMAKE_INCLUDE_FLAG_${LANG} "-I")
	set(CMAKE_${LANG}_COMPILE_OPTIONS_PIC "-fpic")
	set(CMAKE_SHARED_LIBRARY_${LANG}_FLAGS "")
	set(CMAKE_SHARED_LIBRARY_CREATE_${LANG}_FLAGS "-shared")
	set(CMAKE_SHARED_LIBRARY_LINK_${LANG}_FLAGS "")
	set(CMAKE_SHARED_LIBRARY_SONAME_${LANG}_FLAG "-Wl,-soname,")
	set(CMAKE_SHARED_LIBRARY_RUNTIME_${LANG}_FLAG "-Wl,-rpath,")
	set(CMAKE_SHARED_LIBRARY_RPATH_LINK_${LANG}_FLAG "-Wl,-rpath-link,")
	set(CMAKE_${LANG}_VERBOSE_FLAG "-v")
	set(CMAKE_${LANG}_LINKER_WRAPPER_FLAG "-Wl,")
	set(CMAKE_${LANG}_LINKER_WRAPPER_FLAG_SET ",")
	set(CMAKE_${LANG}_LINK_LIBRARY_FLAG "-l")
	set(CMAKE_${LANG}_LIBRARY_PATH_FLAG "-L")

	if(DEFINED ENV{${LANG_ENV}FLAGS})
		set(CMAKE_${LANG}_FLAGS		$ENV{${LANG_ENV}FLAGS}		CACHE STRING "")
	else(NOT CMAKE_${LANG}_FLAGS)
		set(CMAKE_${LANG}_FLAGS		${CMAKE_${LANG}_FLAGS_INIT}	CACHE STRING "")
	endif()

	set(CMAKE_${LANG}_FLAGS_DEBUG		${CMAKE_${LANG}_FLAGS_DEBUG_INIT}		CACHE STRING "")
	set(CMAKE_${LANG}_FLAGS_MINSIZEREL	${CMAKE_${LANG}_FLAGS_MINSIZEREL_INIT}		CACHE STRING "")
	set(CMAKE_${LANG}_FLAGS_RELEASE		${CMAKE_${LANG}_FLAGS_RELEASE_INIT}		CACHE STRING "")
	set(CMAKE_${LANG}_FLAGS_RELWITHDEBINFO	${CMAKE_${LANG}_FLAGS_RELWITHDEBINFO_INIT}	CACHE STRING "")

	if(NOT CMAKE_MODULE_EXISTS)
		set(CMAKE_SHARED_MODULE_${LANG}_FLAGS ${CMAKE_SHARED_LIBRARY_${LANG}_FLAGS})
		set(CMAKE_SHARED_MODULE_CREATE_${LANG}_FLAGS ${CMAKE_SHARED_LIBRARY_CREATE_${LANG}_FLAGS})
	endif()

	if(NOT CMAKE_${LANG}_CREATE_SHARED_LIBRARY)
		set(CMAKE_${LANG}_CREATE_SHARED_LIBRARY "<CMAKE_${LANG}_COMPILER> <CMAKE_SHARED_LIBRARY_${LANG}_FLAGS> <LANGUAGE_COMPILE_FLAGS> <LINK_FLAGS> <CMAKE_SHARED_LIBRARY_CREATE_${LANG}_FLAGS> <SONAME_FLAG><TARGET_SONAME> -o <TARGET> <OBJECTS> <LINK_LIBRARIES>")
	endif()

	if(NOT CMAKE_${LANG}_CREATE_SHARED_MODULE)
		set(CMAKE_${LANG}_CREATE_SHARED_MODULE ${CMAKE_${LANG}_CREATE_SHARED_LIBRARY})
	endif()

	if(NOT DEFINED CMAKE_${LANG}_ARCHIVE_CREATE)
		set(CMAKE_${LANG}_ARCHIVE_CREATE "${CMAKE_${LANG}_COMPILER_AR} qc <TARGET> <LINK_FLAGS> <OBJECTS>")
	endif()

	if(NOT DEFINED CMAKE_${LANG}_ARCHIVE_APPEND)
		 set(CMAKE_${LANG}_ARCHIVE_APPEND "${CMAKE_${LANG}_COMPILER_AR} q <TARGET> <LINK_FLAGS> <OBJECTS>")
	endif()

	if(NOT DEFINED CMAKE_${LANG}_ARCHIVE_FINISH)
		set(CMAKE_${LANG}_ARCHIVE_FINISH "${CMAKE_${LANG}_COMPILER_RANLIB} <TARGET>")
	endif()

	if(NOT CMAKE_${LANG}_COMPILE_OBJECT)
		set(CMAKE_${LANG}_COMPILE_OBJECT "<CMAKE_${LANG}_COMPILER> ${X} <DEFINES> <INCLUDES> <FLAGS> -o <OBJECT> -c <SOURCE>")
	endif()

	if(NOT CMAKE_${LANG}_LINK_EXECUTABLE)
		set(CMAKE_${LANG}_LINK_EXECUTABLE "<CMAKE_${LANG}_COMPILER> <FLAGS> <CMAKE_${LANG}_LINK_FLAGS> <LINK_FLAGS> <OBJECTS> -o <TARGET> <LINK_LIBRARIES>")
	endif()

	if(NOT CMAKE_EXECUTABLE_RPATH_LINK_${LANG}_FLAG)
		set(CMAKE_EXECUTABLE_RPATH_LINK_${LANG}_FLAG ${CMAKE_SHARED_LIBRARY_RPATH_LINK_${LANG}_FLAG})
	endif()

	set(CMAKE_${LANG}_INFORMATION_LOADED 1)

endmacro()

